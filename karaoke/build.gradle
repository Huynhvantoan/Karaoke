apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-allopen'
//apply plugin: 'androidx.navigation.safeargs'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    compileSdkVersion build_versions.compileSdk
    buildToolsVersion build_versions.build_tool
    defaultConfig {
        applicationId build_versions.app_id
        minSdkVersion build_versions.minSdk
        targetSdkVersion build_versions.targetSdk
        versionCode build_versions.versionCode
        versionName build_versions.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        sourceSets.main {
            jniLibs.srcDir 'src/main/libs'
            jni.srcDirs = []
        }
    }

    if(projects.hasProperty('devBuild')){
        splits.splits.abi.enable = false
        splits.splits.density.enable = false
        splits.aaptOptions.cruncherEnabled =false
    }

    signingConfigs {
        Key {
            try {
                storeFile file(key_store.keystore)
                storePassword key_store.keystorePass
                keyAlias key_store.alias
                keyPassword key_store.aliasPass
            }
            catch (ignored) {
                throw new InvalidUserDataException("You should define KEYSTORE_PASSWORD and KEY_PASSWORD in gradle.properties.")
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }
        release {
            signingConfig signingConfigs.Key
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    key_store.proguard_rules, key_store.proguard_rx, key_store.proguard_square, key_store.proguard_support
        }
    }

    flavorDimensions "content"
    productFlavors {
        uHotelDev {
            minSdkVersion build_versions.min_sdk
            buildConfigField "String", "BASE_URL", '"https://skymusic.com.vn/api/"'
            buildConfigField "String", "HOST", '"identify-ap-southeast-1.acrcloud.com"'
            buildConfigField "String", "ACCESS_KEY", '"c5f8665809ec9f785424fb1b837ac283"'
            buildConfigField "String", "ACCESS_SECRET", '"JOfSDjCUb7ITdQK9JHykSVIqPmJeFM3RLc6RgSzY"'
            buildConfigField "String", "CLIENT_KEY", '"kct171101"'
            buildConfigField "String", "CLIENT_SECRET", '"e4d5962539f7a6fa691e1cfb81b73cf3"'
            dimension "content"
        }
        uHotelRelease {
            buildConfigField "String", "BASE_URL", '"https://skymusic.com.vn/api/"'
            buildConfigField "String", "HOST", '"identify-ap-southeast-1.acrcloud.com"'
            buildConfigField "String", "ACCESS_KEY", '"c5f8665809ec9f785424fb1b837ac283"'
            buildConfigField "String", "ACCESS_SECRET", '"JOfSDjCUb7ITdQK9JHykSVIqPmJeFM3RLc6RgSzY"'
            buildConfigField "String", "CLIENT_KEY", '"kct171101"'
            buildConfigField "String", "CLIENT_SECRET", '"e4d5962539f7a6fa691e1cfb81b73cf3"'
            dimension "content"
        }
    }

    lintOptions {
        textOutput "stdout"
        textReport true
        checkAllWarnings true
        warningsAsErrors true
        showAll true
        explainIssues true
        abortOnError false
        lintConfig file("$projectDir/lint.xml")
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'LICENSE.txt'
        exclude 'META-INF/license/LICENSE.base64.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/MANIFEST.MF'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dexOptions {
        maxProcessCount 4
        preDexLibraries false
        javaMaxHeapSize "8g"
    }
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def fileName = "karaoke_v${defaultConfig.versionName}_${getDate()}.apk"
                outputFileName = fileName
            }
        }
    }
    sourceSets {
        main {
            res.srcDirs = [
                    'src/main/res-main',
                    file('src/main/res-screen')
                            .listFiles()
                            .each { res.srcDirs += it.path }
            ]
        }
    }
    androidExtensions {
        experimental = true
    }
    dataBinding {
        enabled = true
    }
    task ndkBuild(type: Exec) {
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        def ndkDir = properties.getProperty('ndk.dir')
        commandLine "$ndkDir/ndk-build", '-B', '-C', file('src/main/jni').absolutePath
    }
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':Core')
    // Kotlin
    implementation deps.kotlin.stdlib
    //Core
    for (dependency in deps.core) {
        implementation dependency.value
    }
    // Architecture components
    for (dependency in deps.lifecycle) {
        implementation dependency.value
    }
    // Dagger 2
    for (dependency in deps.dagger) {
        implementation dependency.value
    }
    // Navigation
    for (dependency in deps.navigation) {
        implementation dependency.value
    }
    //Compiler
    for (dependency in deps.compiler) {
        kapt dependency.value
    }
    //Retrofit
    for (dependency in deps.retrofit) {
        implementation dependency.value
    }
    implementation (deps.libCore.ok_httpLogging,{
        exclude group: 'org.json', module: 'json'
    })
    implementation (deps.libCore.glide_recycler,{
        transitive = false
    })
    //Image Loading
    for (dependency in deps.glide) {
        implementation dependency.value
    }
    //Utils
    for (dependency in deps.utils) {
        implementation dependency.value
    }
    //Library
    for (dependency in deps.library) {
        implementation dependency.value
    }
    // Test
    //implementation deps.multidex
    implementation deps.test.leakcanary
    releaseImplementation deps.test.leakcanary_no_op
    testImplementation deps.test.leakcanary_no_op
    implementation deps.test.blockDebug
    releaseImplementation deps.test.blockRelease
}

@SuppressWarnings("GrMethodMayBeStatic")
def getDate() {
    new Date().format('MMddyyyy')
}